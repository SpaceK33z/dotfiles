# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

fpath+=$ZSH/lib/pure

autoload -U promptinit; promptinit
prompt pure

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
# TODO: preferably I'd like to use $ZSH/**/*.zsh - but I want to skip lib/ folder and too lazy to figure out how to skip one folder
config_files=($ZSH/{fasd,git,macos,system,xcode}/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# enable syntax highlighting
source $ZSH/lib/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
# enable auto suggestions
source $ZSH/lib/zsh-autosuggestions/zsh-autosuggestions.zsh

export PATH="$HOME/.yarn/bin:$HOME/.config/yarn/global/node_modules/.bin:$PATH"
